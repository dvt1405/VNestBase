apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
//apply plugin: 'com.github.dcendents.android-maven'

group='com.github.kizitonwose'
def artifact = 'CalendarView'

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.2"
    defaultConfig {
        minSdkVersion minSdk_version
        targetSdkVersion targetSdk_version
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
    }
    kotlinOptions {
        jvmTarget = "1.8"
        freeCompilerArgs += ['-module-name', "$group.$artifact"]
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation appDependencies.coreKtx

    // Expose ThreeTenABP so library users can use it directly.
    api "com.jakewharton.threetenabp:threetenabp:$threetenabp"

    // Expose RecyclerView which is CalendarView's superclass to
    // prevent a compile error when using the library in a project:
    // Error: Supertypes of the following classes cannot be resolved.
    // Please make sure you have the required dependencies in the classpath:
    // class com.kizitonwose.calendarview.CalendarView,
    // unresolved supertypes: androidx.recyclerview.widget.RecyclerView

    // Not exposing RecyclerView also means that calling findViewById<CalendarView>
    // results in an error: Type argument is not within its bounds, Expected: View.
    // This is because CalendarView's superclass type cannot be determined.
    implementation appDependencies.recyclerView

    testImplementation 'junit:junit:4.12'
    testImplementation('org.threeten:threetenbp:1.4.4') {
        // Use threetenBP library for tests as context will
        // not be available to initialise threetenABP
        exclude group: 'com.jakewharton.threetenabp', module: 'threetenabp'
    }
}

